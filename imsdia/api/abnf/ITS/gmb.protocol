 /**
 * 3GPP TS 29.061 V8.2.0 (2009-03) -- Gmb Interface --Diameter Based Protocol
 * The Gmb reference point is between Between GGSN and BM-SC
 * RFC 3588 - Diameter Base and Accounting Protocol
 * RFC 4006 - Diameter Credit Control Application
 */
   protocol diameter.gmb < 16777223  10415 : "3GPP TS 29.061 V8.2.0 (2009-03)" >;

//////////////////
// Derived AVPs //
//////////////////
/**
 * The Called-Station-Id AVP (AVP Code 30) is of type UTF8String and allows
 * the NAS to send the ASCII string describing the layer 2 address the user
 * contacted in the request. For dialup access,this can be a phone number,
 * obtained using Dialed Number identification (DNIS) or a similar
 * technology. Note that this may be different from the phone number the call
 * comes on. For the use with IEEE 802 access,the Called-Station-Id MAY contain
 * a MAC address formatted as described in RAD802.1X.
 */
   Called-Station-Id ::= 30  /* 29.234 V7.4.0  Section  4.5  */  UTF8String    M|P| |  V  |Y;

/**
 * The Calling-Station-Id AVP (AVP Code 31) is of type UTF8String an allows the
 * NAS to send the ASCII string describing the layer 2 address from which the
 * user connected in the request. For dialup access, this is the phone number
 * the call came from, using Automatic Number Identification (ANI) or a similar
 * technology. For use with IEEE 802 access, the Calling-Station-Id AVP MAY
 * contain a MAC address, formated as described in [RAD802.1X]. It SHOULD only
 * be present in authentication and/or authorization requests.
 */
   Calling-Station-Id   ::= 31  /*29.234 V7.4.0  Section  4.6 */   UTF8String    M|P| |  V |Y;

/**
 * The Framed-IPv6-Prefix AVP (AVP Code 97) is of type OctetString and
 * contains the IPv6 prefix to be configured for the user. One or more AVPs
 * MAY be used in authorization requests as hint to the server that
 * specific IPv6 prefixes are desired, but the server is not required to
 * honor the hint in the corresponding response.
 */
   Framed-IPv6-Prefix    ::= 97  /*29.234 V7.4.0  Section 6.11.6*/  OctetString   M|P| |  V  |Y;


/**
 * The Framed-Interface-Id AVP (AVP Code 96) is of type Unsigned64 and
 * contains the IPv6 interface identifier to be configured for the user.
 * It MAY be used in authorization requests as a hint to the server that
 * a specific interface id is desired, but the server is not required to
 * honor the hint in the corresponding response.
 */
   Framed-Interface-Id      ::= 96 /*29.234 V7.4.0  Section 6.11.5*/   Unsigned64    M|P| |  V | Y;


   Gmb-3GPP-IMSI ::= 1002    /* 3GPP TS 29.061     */ UTF8String   M,V| | |     |N;

/**
 * RAIdentity AVP is of type UTF8String and contains the Routing Area Identity 
 * of the SGSN where the UE  is registered. RAI use and structure is specified 
 * in 3GPP TS 23.003.
 */
   RAI   ::= 909  /* 17.7.12*/ UTF8String   V,M|P| |     |Y; /* 3GPP TS 29.061-930 */

   Gmb-3GPP-IMEISV ::=20 /* 3GPP TS 29.061 V8.2.0*/ OctetString  V |P| |M| Y ;

/**
 * 3GPP-RAT-Type AVP is of type OctetString. It indicates which Radio Access Technology is
 * currently serving the UE.
 */
   Gmb-3GPP-RAT-Type    ::= 21   /*3GPP TS 29.061 V6.7  Section 16.4.7  */ OctetString M,V|P| |     |Y;

/**
 * 3GPP-User-Location-Info AVP is of type Octetstring. It indicates details of where the
 * UE is currenly located (e.g SAI or CGI).
 */
   Gmb-3GPP-User-Location-Info  ::= 22   /* 3GPP TS 29.061 V6.7  Section 16.4.7  */ OctetString
                                                                             M,V|P| |     |Y;

/**
 * 3GPP-MS-TimeZone AVP is of type OctetString. It indicates the offset between universal
 * time and local time in steps of 15 minutes of where the MS currently resides.
 */
   Gmb-3GPP-MS-TimeZone  ::= 23   /*  3GPP TS 29.061 V6.7  Section 16.4.7  */ OctetString                                                                            M,V|P| |     |Y;

/**
 * The Additional-MBMS-Trace-Info AVP (AVP Code 910) is of type OctetString. This AVP contains 
 * Trace Reference2, Trace Recording Session Reference, Triggering Events in BM-SC, Trace Depth 
 * for BM-SC, List of interfaces in BMSC,Trace Activity Control For BM-SC which are all part of 
 * the Additional MBMS Trace Info IE .
 */

   Additional-MBMS-Trace-Info ::=910 /* 3GPP TS 29.061 V8.2.0 17.7.13 */OctetString M,V| P| | | Y;

/**
 * The Alternative-APN AVP (AVP code 905) is of type UTF8String, and contains the value of a 
 * new APN. This AVP is optional within the Gmb interface. BM-SC only includes it if the UE 
 * must use a different APN for the MBMS PDP Context from the one used in the Join message.
 */
   Alternative-APN::=905 /* 3GPP TS 29.061 V8.2.0  17.7.8  */ UTF8String    M,V| P| | | Y;

/**
 * The TMGI AVP (AVP code 900) is of type OctetString, and contains the Temporary Mobile Group 
 * Identity allocated to a particular MBMS bearer service. It is allocated by the BM-SC.
 */
   TMGI ::=900 /* 3GPP TS 29.061 V8.2.0 17.7.2 */  OctetString   M,V| P| | | Y;

/**
 * The Required-MBMS-Bearer-Capabilities AVP (AVP code 901) is of type UTF8String, and contains 
 * the minimum bearer capabilities the UE needs to support. The information contained in this AVP 
 * is UTF-8 encoded MBMS bearer capabilities
 */
   Required-MBMS-Bearer-Capabilities::=901 /* 3GPP TS 29.061 V8.2.0  17.7.3 */ UTF8String
                                                                  M,V| P| | | Y;

/**
 * The MBMS-StartStop-Indication AVP (AVP code 902) is of type Enumerated .
*/
   MBMS-StartStop-Indication::=902 /*3GPP TS 29.061 V8.2.0   17.7.5 */  Enumerated  M,V| P| | | Y;

/**
 * MBMS-Service-Area AVP is of type OctetString and indicates the area over which the MBMS 
 * bearer service has to be distributed.
*/
   MBMS-Service-Area   ::= 903  /* 3GPP TS 29.061 V6.7   17.7.6 */ OctetString  V,M|P| |     |Y;

/**
 * The MBMS-Required-QoS AVP (AVP code 913) is the quality of service required for the 
 * MBMS bearer service. It is specified as UTF8String with the following encoding:
*/
   MBMS-Required-QoS ::=913 /* 3GPP TS 29.061 V6.7 17.7.16  */ UTF8String  V,M|P| |     |Y;

/**
 * The MBMS-Session-Duration AVP (AVP code 904) is of type OctetString with a length of three 
 * octets and indicates the estimated session duration (MBMS Service data transmission).
*/
   MBMS-Session-Duration ::= 904/* 3GPP TS 29.061 V6.7 17.7.7*/ OctetString V,M|P| |     |Y;

/**
 * The MBMS-Service-Type AVP (AVP code 906) is of type Enumerated, and contains explicit 
 * information about the type of service that the BM-SC Start Procedure is about to start.
*/
   MBMS-Service-Type::= 906 /* 3GPP TS 29.061 V6.7  17.7.9 */Enumerated V,M|P| |     |Y;
/**
 * The MBMS-Counting-Information AVP (AVP code 914) is of type Enumerated, and contains 
 * explicit information about whether the MBMS Counting procedures are applicable for 
 * the MBMS Service that is about to start.
*/
   MBMS-Counting-Information::= 914/* 3GPP TS 29.061 V6.7 17.7.17*/Enumerated V,M|P| |     |Y;
 
/**
 * The MBMS-Session-Identity AVP (AVP code 908) is of type OctetString. Its length is one octet. 
 * iIt is allocated by the BM-SC. Together with TMGI it identifies a transmission of a specific 
 * MBMS session. The initial transmission and subsequent retransmissions of the MBMS session 
 * will use the same values of these parameters. This AVP is optional within the Gmb interface.
*/

   MBMS-Session-Identity::= 908/* 3GPP TS 29.061 V6.7  17.7.11  */OctetString  V,M|P| |     |Y;

/**
 * The MBMS-Session-Repetition-Number AVP (AVP code 912) is of type OctetString with a length 
 * of one octet. It contains the session identity repetition number of the MBMS transmission 
 * session on the Gmb interface.
*/
   MBMS-Session-Repetition-number::= 912/* 3GPP TS 29.061 V6.7  17.7.15*/OctetString  
										V,M|P| |     |Y;

/**
 * 3GPP-SGSN-Address AVP (AVP Code 6) is of type UTF8String and contains the
 * SGSN IP address that is used by the GTP control plane for the handling of
 * control messages. It may be used to identify the PLMN to which the user is
 * attached.
 */
    Gmb-3GPP-SGSN-Address ::= 6 /*3GPP TS 29.061 V7.0 Section  16.4.7 */ UTF8String  M,V|P| |   |Y;
   
/**
 * For GGSN, it represents the SGSN IPv6 address that is used by the GTP control plane for the
 * handling of control messages. For P-GW, it represents the IPv6 address of the S-GW, 
 * trusted non-3GPP IP access or  ePDG that is used on S5/S8, S2a or S2b for the handling of 
 * control messages. The address may be used to identify the PLMN to which the user is attached. 
*/
   Gmb-3GPP-SGSN-IPv6-Address   ::=15  /* 3GPP TS 29.061 V8.2.0*/ OctetString  V |P| |M| Y ;

/**
 * The MBMS-2G-3G-Indicator AVP (AVP code 907) is of type Enumerated. It indicates whether 
 * the MBMS bearer service will be delivered in 2G- only, 3G- only of both coverage areas.
*/

   MBMS-2G-3G-Indicator::=907 /* 3GPP TS 29.061 V8.2.0  17.7.10 */Enumerated V |P| |M| Y ;

/**
 * The MBMS-Time-To-Data-Transfer AVP (AVP code 911) is of type OctetString. Its length is 
 * one octet. It indicates the expected time between reception of the MBMS Session Start 
 * (RAR(Start) command) and the commencement of the MBMS Data flow.
*/
   MBMS-Time-To-Data-Transfer::=911 /* 3GPP TS 29.061 V8.2.0 17.7.14 */ OctetString
                                                                    V |P| |M| Y ;
/**
 * The MBMS-User-Data-Mode-Indication AVP (AVP code 915) is of type Enumerated. The meaning 
 * of the message containing this AVP depends on the sending entity. The absence of this AVP 
 * indicates unicast mode of operation.
*/ 
   MBMS-User-Data-Mode-Indication::=915/* 3GPP TS 29.061 V8.2.0  17.7.18*/Enumerated V |P| |M| Y ;


/**
 * The MBMS-BMSC-SSM-IP-Address AVP (AVP code 918) is of type OctetString, and contains the value
 * of BM-SC's IPv4 address for Source Specific Multicasting. IPv4 only or dual stack BM-SC 
 * includes this AVP.
*/
   MBMS-BMSC-SSM-IP-Address ::=918 /*3GPP TS 29.061 V8.2.0 17.7.21 */ OctetString  V |P| |M| Y ;

/**
 * The MBMS-BMSC-SSM-IPv6-Address AVP (AVP code 919) is of type OctetString, and contains the 
 * value of BMSC's IPv4 address for Source Specific Multicasting. Dual stack BM-SC includes 
 * this AVP.
*/
   MBMS-BMSC-SSM-IPv6-Address::=919 /*3GPP TS 29.061 V8.2.0 17.7.22 */ OctetString  V |P| |M| Y ;

/**
 * The MBMS-Flow-Identifier AVP (AVP code 920) is of type OctetString. Its length is two octets. 
 * It represents a location dependent subflow of a broadcast MBMS bearer service. When present, 
 * the MBMS-Flow-Identifier together with the TMGI uniquely identify an MBMS Bearer Context.
*/
   MBMS-Flow-Identifier::=920/*3GPP TS 29.061 V8.2.0  17.7.23 */ OctetString  V |P| |M| Y ;
   
/**
 * The MBMS-GGSN-Address AVP (AVP code 916) is of type OctetString, and contains the value 
 * of GGSN's IPv4 address for user plane data. IPv4 only or dual stack GGSN includes this 
 * AVP in case BM-SC requests GGSN's user plane unicast address
*/

   MBMS-GGSN-Address::=916/*3GPP TS 29.061 V8.2.0 17.7.19*/ OctetString  V |P| |M| Y ;
/**
 * The MBMS-GGSN-IPv6-Address AVP (AVP code 917) is of type OctetString, and contains the 
 * value of GGSN's IPv4 address for user plane data. Dual stack GGSN includes this AVP in 
 * case BM-SC requests GGSN's user plane unicast address.
*/
   MBMS-GGSN-IPv6-Address::= 917/*3GPP TS 29.061 V8.2.0 17.7.20*/ OctetString  V |P| |M| Y ;


///////////////
//Enumearted///
///////////////

   MBMS-StartStop-Indication ::= < Enumerated :902 >
                                   Gmb_START        0
                                   Gmb_STOP         1
                                   Gmb_UPDATE       2
                                   ;

   MBMS-Service-Type         ::= < Enumerated :906>
                                   MULTICAST       0
                                   BROADCAST       1
                                   ; 
   MBMS-Counting-Information ::= < Enumerated :914 >
                                   COUNTING_NOT_APPLICABLE    0
                                   COUNTING_APPLICABLE        1
                                   ;
   MBMS-2G-3G-Indicator      ::= < Enumerated :907 >
                                   Gmb_2G                0
                                   Gmb_3G                1
                                   Gmb_2G_AND_Gmb_3G     2
                                   ;
   MBMS-User-Data-Mode-Indication ::=< Enumerated :915 >
                                       Gmb_Unicast                     0
                                       Gmb_Multicast_and_Gmb_Unicast   1
                                       ;


///////////////
//commands/////
///////////////

/**
 * The AA-Request (AAR), which is indicated by setting the Command-Code field to
 * 265 and the 'R' bit in the Command Flags field, is used to request
 * authentication and/or authorization for a given NAS user.
 * The type of request is identified through the Auth-Request-Type AVP [BASE].
 * The recommended value for most RADIUS interoperabily situations is
 * AUTHORIZE_AUTHENTICATE.
 * If Authentication is requested, the User-Name attribute SHOULD be present, as
 * well as any additional authentication AVPs that would carry the password
 * information. A request for authorization SHOULD only include the information
 * from which the authorization will be performed, such as the User-Name,
 * Called-Station-Id, or Calling-Station-Id AVPs. All requests SHOULD contain
 * AVPs uniquely identifying the source of the call, such as Origin-Host and
 * NAS-Port. Certain networks MAY use different AVPs for authorization purposes.
 * A request for authorization will include some AVPs defined in section 6.
 */

	AA-Request ::=  < Diameter Header: 265, REQ, PXY >
			< Session-Id >
			{ Auth-Application-Id }
			{ Origin-Host }
			{ Origin-Realm }
			{ Destination-Realm }
			{ Auth-Request-Type }
			[ Destination-Host ]
			[ Called-Station-Id ]
			[ Calling-Station-Id ]
			[ Framed-IP-Address]
			[ Framed-IPv6-Prefix ]
			[ Framed-Interface-Id ]
			* [ Proxy-Info ]
			* [ Route-Record ]
			[ Gmb-3GPP-IMSI]
			[ RAI ]
			[ Gmb-3GPP-IMEISV ]
			[ Gmb-3GPP-RAT-Type ]
			[ Gmb-3GPP-User-Location-Info ]
			[ Gmb-3GPP-MS-TimeZone ]
			[ Additional-MBMS-Trace-Info ]
			* [ AVP ]
			;	

/**
 * The AA-Answer (AAA) message is indicated by setting the Command-Code field to
 * 265 and clearing the 'R' bit in the Command Flags field. It is sent in
 * response to the AA-Request (AAR) message. If authorization was requested, a
 * successful response will include the authorization AVPs appropriate for the
 * service being provided, as defined in section 6.
 * For authentication exchanges requiring more than a single round trip,
 * the server MUST set the Result-Code AVP to DIAMETER_MULTI_ROUND_AUTH.
 * An AAA message with this result code MAY include one Reply-Message or
 * more and MAY include zero or one State AVPs.
 */

	AA-Answer ::= 	< Diameter Header: 265, PXY >
			< Session-Id >
			{ Auth-Application-Id }
			{ Origin-Host }
			{ Origin-Realm }
			[ Result-Code ]
			[ Experimental-Result ]
			[ Error-Message ]
			[ Error-Reporting-Host ]
			* [ Failed-AVP ]
			* [ Proxy-Info ]
			[ Alternative-APN ]
			[ Gmb-3GPP-IMSI]
			[ TMGI ]
			[ Required-MBMS-Bearer-Capabilities ]
			* [ AVP ]
			;

/**
 * The Session-Termination-Request (STR), indicated by the Command-Code
 * set to 275 and the Command Flags' 'R' bit set, is sent by the access
 * device to inform the Diameter Server that an authenticated and/or
 * authorized session is being terminated.
 */


	ST-Request ::=  < Diameter Header: 275, REQ, PXY >
			< Session-Id >
			{ Origin-Host }
			{ Origin-Realm }
			{ Destination-Realm }
			{ Auth-Application-Id }
			{ Termination-Cause }
			[ Destination-Host ]
			* [ Class ]
			[ Origin-State-Id ]
			* [ Proxy-Info ]
			* [ Route-Record ]
			[ Additional-MBMS-Trace-Info ]
			* [ AVP ]
			;
/**
 * The Session-Termination-Answer (STA), indicated by the Command-Code
 * set to 275 and the message flags' 'R' bit clear, is sent by the
 * Diameter Server to acknowledge the notification that the session has
 * been terminated.  The Result-Code AVP MUST be present, and MAY
 * contain an indication that an error occurred while servicing the STR.
 * Upon sending or receipt of the STA, the Diameter Server MUST release
 * all resources for the session indicated by the Session-Id AVP.  Any
 * intermediate server in the Proxy-Chain MAY also release any
 * resources, if necessary.
 */

	ST-Answer ::=   < Diameter Header: 275, PXY >
			< Session-Id >
			{ Result-Code }
			{ Origin-Host }
			{ Origin-Realm }
			* [ Class ]
			[ Error-Message ]
			[ Error-Reporting-Host ]
			* [ Failed-AVP ]
			[ Origin-State-Id ]
			* [ Redirect-Host ]
			[ Redirect-Host-Usage ]
			[ Redirect-Max-Cache-Time ]
			* [ Proxy-Info ]
			* [ AVP ]
			;

/**
 * A Diameter server may initiate a re-authentication and/or reauthorization
 * service for a particular session by issuing a Re-Auth-Request (RAR) message
 * [BASE].For example, for pre-paid services, the Diameter server that
 * originally authorized a session may need some confirmation that the user is
 * still using the services. If a NAS receives an RAR message with Session-Id
 * equal to a currently active session and a Re-Auth-Type that includes
 * authentication, it MUST initiate a re-authentication toward the user, if
 * the service supports this particular feature.
 */

	RAR ::=         < Diameter Header: 258, REQ, PXY >
			< Session-Id >
			{ Origin-Host }
			{ Origin-Realm }
			{ Destination-Realm }
			{ Destination-Host }
			{ Auth-Application-Id }
			{ Re-Auth-Request-Type }
			[ Called-Station-Id ]
			[ Framed-IP-Address]
			[ Framed-IPv6-Prefix ]
			[ Framed-Interface-Id ]
			[ MBMS-StartStop-Indication ]   
			[ MBMS-Service-Area ]
			[ MBMS-Required-QoS ]
			[ MBMS-Session-Duration ]
			[ MBMS-Service-Type ]
			[ MBMS-Counting-Information ]
			[ MBMS-Session-Identity ]
			[ MBMS-Session-Repetition-number ]
			[ TMGI ]
			* [ Gmb-3GPP-SGSN-Address ] 
			* [ Gmb-3GPP-SGSN-IPv6-Address ]
			[ MBMS-2G-3G-Indicator ]
			[ MBMS-Time-To-Data-Transfer ]
			[ MBMS-User-Data-Mode-Indication ]
			[ MBMS-BMSC-SSM-IP-Address ]
			[ MBMS-BMSC-SSM-IPv6-Address ]
			[ MBMS-Flow-Identifier ]
			[ Origin-State-Id ]
			* [ Proxy-Info ]
			* [ Route-Record ]
			* [ AVP ]
			;


/**
 * The Re-Auth-Answer (RAA) message [BASE] is sent in response to the RAR. The
 * Result-Code AVP MUST be present and indicates the disposition of the request.
 * A successful RAA transaction MUST be followed by an AAR message.
 */

	RAA ::=         < Diameter Header: 258, PXY >
			< Session-Id >
			{ Origin-Host }
			{ Origin-Realm }
			[ Result-Code ]
			[ Experimental-Result ]
			[ MBMS-StartStop-Indication ]
			[ MBMS-GGSN-Address ] 
			[ MBMS-GGSN-IPv6-Address ] 
			[ MBMS-User-Data-Mode-Indication ]
			[ Origin-State-Id ]
			[ Error-Message ]
			[ Error-Reporting-Host ]
			* [ Failed-AVP ]
			* [ Redirect-Host ]
			[ Redirect-Host-Usage ]
			[ Redirect-Max-Cache-Time ]//in spec mentioned as Redirect-Host-Cache-Time.
                        * [ Proxy-Info]
			* [ AVP ]
			;
/**
 * The Abort-Session-Request (ASR), indicated by the Command-Code set to
 * 274 and the message flags' 'R' bit set, may be sent by any server to
 * the access device that is providing session service, to request that
 * the session identified by the Session-Id be stopped.
 */
	ASR ::=         < Diameter Header: 274, REQ, PXY >
			< Session-Id >
			{ Origin-Host }
			{ Origin-Realm }
			{ Destination-Realm }
			{ Destination-Host }
			{ Auth-Application-Id }
			[ Origin-State-Id ]
			* [ Proxy-Info ]
			* [ Route-Record ]
			* [ AVP ]
			;

/**
 * The Abort-Session-Answer (ASA), indicated by the Command-Code set to
 * 274 and the message flags' 'R' bit clear, is sent in response to the
 * ASR.  The Result-Code AVP MUST be present, and indicates the
 * disposition of the request.
 * If the session identified by Session-Id in the ASR was successfully
 * terminated, Result-Code is set to DIAMETER_SUCCESS.  If the session
 * is not currently active, Result-Code is set to
 * DIAMETER_UNKNOWN_SESSION_ID.  If the access device does not stop the
 * session for any other reason, Result-Code is set to
 * DIAMETER_UNABLE_TO_COMPLY.
 */

	ASA ::= 	< Diameter Header: 274, PXY >
			< Session-Id >
			{ Result-Code }
			{ Origin-Host }
			{ Origin-Realm }
			[ Origin-State-Id ]
			[ Error-Message ]
			[ Error-Reporting-Host ]
			* [ Failed-AVP ]
			* [ Redirect-Host ]
			[ Redirect-Host-Usage ]
			[ Redirect-Max-Cache-Time ]
			* [ Proxy-Info ]
			* [ AVP ]
			;

