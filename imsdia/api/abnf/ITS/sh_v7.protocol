/**
 * ETSI TS 129 329 V7.1.0 (2006-03) - Sh interface --Diameter Based Protocol
 * <p>
 * Sh interface between an AS and the HSS<br>
 * Sh interface between an SCS and the HSS<br>
 */
protocol diameter.sh_v7 < 16777217 10415: "ETSI TS 129 329 V7.1.0 (2006-03)" >;


/**
 * This is the dictionary Id used by DTF. This has to be unique.
 * Refer to the excel "Diameter_Dictionary_Details.xlsx" for
 * currently used ID's to avoid collisions.
 */
dict-id 17;

//////////////////
// Derived AVPs //
//////////////////

Sh-User-Data                                      ::=  702 /*6.3.3*/  OctetString M,V| | |    |N;

/**
 * The User-Identity AVP is of type Grouped. This AVP contains either a Public- Identity AVP or an MSISDN AVP.
 */
User-Identity                                     ::=  700 /*6.3.1*/  Grouped     M,V| | |    |N;

/**
 * The MSISDN AVP is of type OctetString. This AVP contains an MSISDN, in international number format as described
 * in ITU-T Rec E.164, encoded as a TBCD-string, i.e. digits from 0 through 9 are encoded 0000 to 1001; 1111 is used
 * as a filler when there is an odd number of digits; bits 8 to 5 of octet n encode digit 2n; bits 4 to 1 of octet n encode digit
 * 2(n-1)+1.
 */
MSISDN                                            ::=  701 /*6.3.2*/  OctetString M,V| | |    |N;

/**
 * The Data-Reference AVP is of type Enumerated, and indicates the type of the requested user data in the operation UDR
 * and SNR. Its exact values and meaning is defined in 3GPP TS 29.328. The following values are defined (more
 * details are given in 3GPP TS 29.328):
 * <ul>
 * <li> RepositoryData (0)
 * <li> IMSPublicIdentity (10)
 * <li> IMSUserState (11)
 * <li> S-CSCFName (12)
 * <li> InitialFilterCriteria (13)<br>
 * This value is used to request initial filter criteria relevant to the requesting AS
 * <li> LocationInformation (14)
 * <li> UserState (15)
 * <li> ChargingInformation (16)
 * <li> MSISDN (17)
 * </ul>
 * NOTE: Value 10000 is reserved in Release 6 for use with Early IMS Security, see 3GPP TR 33.978
 */
Data-Reference                                    ::=  703 /*6.3.4*/  Enumerated  M,V| | |    |N;

/**
 * The Service-Indication AVP is of type OctetString. This AVP contains the Service Indication that identifies a service in
 * an AS.
 */
Service-Indication                                ::=  704 /*6.3.5*/  OctetString M,V| | |    |N;

/**
 * The Subs-Req-Type AVP is of type Enumerated, and indicates the type of the subscription-to-notifications request. The
 * following values are defined:
 * <ul>
 * <li>Subscribe (0)<br>
 * This value is used by an AS to subscribe to notifications of changes in data.
 * <li>Unsubscribe (1)<br>
 * This value is used by an AS to unsubscribe to notifications of changes in data.
 * </ul>
 */
Subs-Req-Type                                     ::=  705 /*6.3.6*/  Enumerated  M,V| | |    |N;

/**
 * The Requested-Domain AVP is of type Enumerated, and indicates the access domain for which certain data (e.g. user
 * state) are requested. The following values are defined:
 * <ul>
 * <li>CS-Domain (0)<br>
 * The requested data apply to the CS domain.
 * <li>PS-Domain (1)<br>
 * The requested data apply to the PS domain.
 * </ul>
 */
Requested-Domain                                  ::=  706 /*6.3.7*/  Enumerated  M,V| | |    |N;

/**
 * The Current-Location AVP is of type Enumerated, and indicates whether an active location retrieval has to be initiated
 * or not:
 * <ul>
 * <li>DoNotNeedInitiateActiveLocationRetrieval (0)<br>
 * The request indicates that the initiation of an active location retrieval is not required.
 * <li>InitiateActiveLocationRetrieval (1)<br>
 * It is requested that an active location retrieval is initiated.
 * </ul>
 */
Current-Location                                  ::=  707 /*6.3.8*/  Enumerated  M,V| | |    |N;

/**
 * The Identity-Set AVP is of type Enumerated and indicates the requested set of IMS Public Identities. The following
 * values are defined:
 * <ul>
 * <li>ALL_IDENTITIES (0)
 * <li>REGISTERED_IDENTITIES (1)
 * <li>IMPLICIT_IDENTITIES (2)
 * </ul>
 */
Identity-Set                                      ::=  708 /*6.3.10*/ Enumerated  V  | | | M  |N;

/**
 * The Expiry-Time AVP is of type Time. It contains the expiry time of subscription to 
 * notifications in the HSS. 
 */
Expiry-Time                                       ::=  709 /*6.3.16*/ Time        V  | | | M  |N;



//////////////////
// Enumerations //
//////////////////

Data-Reference    ::= < Enumerated: 703 >
                RepositoryData          0
                IMSPublicIdentity       10
                IMSUserState            11
                SCSCFName               12
                InitialFilterCriteria   13
                LocationInformation     14
                UserState               15
                ChargingInformation     16
                MSISDN                  17
                ;

Subs-Req-Type    ::= < Enumerated: 705 >
                Subscribe               0
                Unsubscribe             1
                ;

Requested-Domain ::= < Enumerated: 706 >
                CSDomain               0
                PSDomain               1
                ;


Current-Location ::= < Enumerated: 707 >
                DoNotNeedInitiateActiveLocationRetrieval        0
                InitiateActiveLocationRetrieval                 1
                ;

Identity-Set     ::= < Enumerated: 708 >
                ALL_IDENTITIES          0
                REGISTERED_IDENTITIES   1
                IMPLICIT_IDENTITIES     2
                ;

//////////////////
// Grouped AVPs //
//////////////////

User-Identity ::= <AVP Header: 700 10415>
	[Public-Identity]
	[MSISDN]
	*[AVP]
        ;



//////////////////
//Commands
/////////////////////

/**
 * The User-Data-Request (UDR) command, indicated by the Command-Code field set to 306 and the <tt>R</tt> bit set in the
 * Command Flags field, is sent by a Diameter client to a Diameter server in order to request user data.
 */
User-Data-Request ::= < Diameter Header: 306, REQ, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		{ Destination-Realm }
		{ User-Identity }
		*{ Data-Reference }
		[ Destination-Host ]
		*[ Supported-Features ]
		[ Server-Name ]
		*[ Service-Indication ]
		[ Identity-Set ]
		[ Requested-Domain ]
		[ Current-Location ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
                ;

/**
 * The User-Data-Answer (UDA) command, indicated by the Command-Code field set to 306 and the <tt>R</tt> bit cleared in
 * the Command Flags field, is sent by a server in response to the User-Data-Request command. The Experimental-Result
 * AVP may contain one of the values defined in section 6.2 or in 3GPP TS 29.229 [6].
 */
User-Data-Answer  ::= < Diameter Header: 306, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		[ Result-Code ]
		[ Experimental-Result ]
		*[ Supported-Features ]
		[ Sh-User-Data ]
		*[ Failed-AVP ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
                ;

/**
 * The Profile-Update-Request (PUR) command, indicated by the Command-Code field set to 307 and the <tt>R</tt> bit set in
 * the Command Flags field, is sent by a Diameter client to a Diameter server in order to update user data in the server.
 */
Profile-Update-Request  ::= < Diameter Header: 307, REQ, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		{ Destination-Realm }
		{ User-Identity }
		{ Data-Reference }
		{ Sh-User-Data }
		[ Destination-Host ]
		*[ Supported-Features ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
		;

/**
 * The Profile-Update-Answer (PUA) command, indicated by the Command-Code field set to 307 and the <tt>R</tt> bit cleared
 * in the Command Flags field, is sent by a server in response to the Profile-Update-Request command. The Experimental-
 * Result AVP may contain one of the values defined in section 6.2 or in 3GPP TS 29.229 [6].
 */
Profile-Update-Answer  ::=< Diameter Header: 307, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		[ Result-Code ]
		[ Experimental-Result ]
		*[ Supported-Features ]
		*[ Failed-AVP ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
                ;

/**
 * The Subscribe-Notifications-Request (SNR) command, indicated by the Command-Code field set to 308 and the <tt>R</tt> bit
 * set in the Command Flags field, is sent by a Diameter client to a Diameter server in order to request notifications of
 * changes in user data.
 */
Subscribe-Notifications-Request  ::= < Diameter Header: 308, REQ, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		{ Destination-Realm }
		{ User-Identity }
		{ Subs-Req-Type }
		*{ Data-Reference }
		[ Destination-Host ]
		*[ Supported-Features ]
		*[ Service-Indication]
		[ Server-Name ]
                [ Expiry-Time ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
                ;

/**
 * The Subscribe-Notifications-Answer command, indicated by the Command-Code field set to 308 and the <tt>R</tt> bit cleared
 * in the Command Flags field, is sent by a server in response to the Subscribe-Notifications-Request command. The
 * Result-Code or Experimental-Result AVP may contain one of the values defined in section 6.2 or in 3GPP TS 29.229
 * [6].
 */
Subscribe-Notifications-Answer  ::= < Diameter Header: 308, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		[ Result-Code ]
		[ Experimental-Result ]
		*[ Supported-Features ]
                [ Expiry-Time ]
		*[ Failed-AVP ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
		;


/**
 * The Push-Notification-Request (PNR) command, indicated by the Command-Code field set to 309 and the <tt>R</tt> bit set in
 * the Command Flags field, is sent by a Diameter server to a Diameter client in order to notify changes in the user data in
 * the server.
 */
Push-Notification-Request  ::= < Diameter Header: 309, REQ, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
                { Origin-Realm }
		{ Destination-Host }
		{ Destination-Realm }
		{ User-Identity }
		{ Sh-User-Data }
		*[ Supported-Features ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]
                ;


/**
 * The Push-Notifications-Answer (PNA) command, indicated by the Command-Code field set to 309 and the <tt>R</tt> bit
 * cleared in the Command Flags field, is sent by a client in response to the Push-Notification-Request command. The
 * Experimental-Result AVP may contain one of the values defined in section 6.2 or in 3GPP TS 29.229 [6].
 */
Push-Notification-Answer  ::=< Diameter Header: 309, PXY, 16777217 >
		< Session-Id >
		{ Vendor-Specific-Application-Id }
		{ Auth-Session-State }
		{ Origin-Host }
		{ Origin-Realm }
		[ Result-Code ]
		[ Experimental-Result ]
		*[ Supported-Features ]
		*[ Failed-AVP ]
		*[ Proxy-Info ]
		*[ Route-Record ]
		*[ AVP ]	
		;


